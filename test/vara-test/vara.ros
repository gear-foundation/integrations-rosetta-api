create_account(1){
  create{
    network = {"network":"testnet", "blockchain":"vara"};
    key = generate_key({"curve_type": "edwards25519"});
    account = derive({
      "network_identifier": {{network}},
      "public_key": {{key.public_key}}
    });
 
    save_account({
      "account_identifier": {{account.account_identifier}},
      "keypair": {{key}}
    });
  }
}

transfer(1){
  transfer{
    transfer.network = {"network":"testnet", "blockchain":"vara"};
    currency = {"symbol":"VARA", "decimals":12};

    sender = find_balance({
      "minimum_balance":{
        "value": "20000000000000",
        "currency": {{currency}}
      }
    });
    
    recipient_amount = random_number({"minimum": "10000000000000", "maximum": "20000000000000"});
    sender_amount = 0 - {{recipient_amount}};
    print_message({"sender":{{sender.account_identifier}}, "amount":{{sender_amount}}});

    recipient = find_balance({
      "not_account_identifier":[{{sender.account_identifier}}],
      "minimum_balance":{
        "value": "0",
        "currency": {{currency}}
      },
      "create_limit": 100
    });

    print_message({"recipient":{{recipient.account_identifier}}, "amount":{{recipient_amount}}});
    
    transfer.confirmation_depth = "16";
    transfer.operations = [
      {
        "operation_identifier":{"index":0},
        "type":"Transfer",
        "account":{{sender.account_identifier}},
        "amount":{
          "value":{{sender_amount}},
          "currency":{{currency}}
        }
      },
      {
        "operation_identifier":{"index":1},
        "type":"Transfer",
        "account":{{recipient.account_identifier}},
        "amount":{
          "value":{{recipient_amount}},
          "currency":{{currency}}
        }
      }
    ];
  }
}
